#To edit and compare internal_properties, use WINDEV integrated tools.
#Internal properties refer to the properties of controls in windows, reports, etc.
info :
 name : pProgram
 major_version : 28
 minor_version : 0
 type : 7
 description : ""
 subtype : 0
procedure_set :
 identifier : 0x11150f6c0056072f
 internal_properties : CAAAAAgAAABGLu41kG7fjQV3iS4F72qmnKaNh5694reolNKIW0iw
 code_elements :
  type_code : 31
  p_codes : []
  procedures :
   -
     name : EXE_UninstallService
     procedure_id : 1230907029692286767
     type_code : 15
     code : |1-
      // Summary: (Executable mode only) Uninstalls the service
      Procedure EXE_UninstallService()
      
      IF InTestMode OR InServiceMode THEN RETURN
      
      // Opens the monitoring window
      OpenChild(WIN_Install)
      
      // Status	
      WIN_Install.STC_INFO = “Stopping the service...”
      Wait(1, waitRedraw)
      
      // Stops the service
      IF ServiceStatus(SERVICE_NAME) = serviceStatusStopped _OR_ ServiceStop(SERVICE_NAME) THEN
      	
      	// Status	
      	WIN_Install.STC_INFO = “Uninstalling the service...”
      	Wait(1, waitRedraw)
      	
      	// Uninstalls the service
      	IF ServiceUninstall(SERVICE_NAME) THEN
      		
      		// Informs the user
      		Info(“The service was successfully uninstalled”)
      	ELSE
      		
      		// Displays the error
      		Error()
      	END
      ELSE
      	
      	// Displays the error
      	Error()	
      END
      
      // Close the monitoring window
      Close(WIN_Install)
      
      // End of program
      EndProgram(True)
     type : 458752
   -
     name : EXE_InstallService
     procedure_id : 1230907029692352303
     type_code : 15
     code : |1-
      // Summary: (Executable mode only) Installs the service
      Procedure EXE_InstallService()
      
      IF InTestMode OR InServiceMode THEN RETURN
      
      // Opens the monitoring window
      OpenChild(WIN_Install)
      
      // Status
      WIN_Install.STC_INFO = “Installing the service...”
      Wait(1, waitRedraw)
      
      // Configuring the service
      Service.LongDescription	= “Example of Windows service developed with WINDEV ” + WinDevVersion(versionVM)[[TO 2]]			// Caption used to describe the service
      Service.Description 		= “PC SOFT Example of WINDEV service”														// Full name of the service
      Service.Type 				= serviceTypeSimple																	// Standard service operating on its own in its process
      Service.Startup 			= serviceStartupAutomatic														// Automatic startup once the system is initialized
      Service.StartupError 	= serviceErrorLog																	// The system writes the failure for starting the service into the log of event observer
      Service.CommandLine 		= “""” + ExeInfo(exeName) + “""”														// Full path of the current executable
      
      // Installs the service
      IF ServiceInstall(SERVICE_NAME) THEN
      	
      	// Status
      	WIN_Install.STC_INFO = “Starting the service...”
      	Wait(1, waitRedraw)
      	
      	// Starts the service
      	IF ServiceStart(SERVICE_NAME) THEN
      		
      		// Informs the user
      		Info(“The service was properly installed”)
      	ELSE
      		
      		// Displays the error
      		Error()
      	END
      ELSE
      	
      	// Displays the error
      	Error()
      END
      
      // Close the monitoring window
      Close(WIN_Install)
      
      // End of program
      EndProgram(True)
     type : 458752
   -
     name : OnCommandLine
     procedure_id : 1230907029692417839
     type_code : 15
     code : |1-
      // Summary: Processes the command line
      Procedure OnCommandLine()
      
      SWITCH CommandLine(1)
      
      	// Parameter to install the service
      	CASE “-i”, “-install”, “install”
      		
      		// The setup of the service cannot be run in service mode
      		IF NOT InServiceMode THEN
      			
      			// The setup of the service cannot be run in test mode
      			IF NOT InTestMode THEN
      				
      				// Starts the setup of the service
      				EXE_InstallService()
      				
      			ELSE
      				
      				// Stop the program
      				EndProgram(“The setup of the service cannot be performed in test mode.”)
      			END
      			
      		END
      		
      	// Parameter to uninstall the service
      	CASE “-u”, “-r”, “-remove”, “remove”, “-uninstall”, “uninstall”
      		
      		// The service cannot be uninstalled in service mode
      		IF NOT InServiceMode THEN
      			
      			// The service cannot be uninstalled in test mode
      			IF NOT InTestMode THEN
      				
      				EXE_UninstallService()
      				
      			ELSE
      				
      				// Stop the program
      				EndProgram(“The service cannot be uninstall in test mode.”)
      			END
      		END
      		
      	OTHER CASE
      		
      		// In test mode
      		IF InTestMode THEN
      			
      			// Stop the program
      			EndProgram(“This project is not intended to be run in test mode. See the code of this example for more details about the execution of a service.”)
      		ELSE
      			
      			// In runtime mode
      			IF NOT InServiceMode THEN
      				
      				// Opens the window for manual setup
      				Open(WIN_Install, True)
      			ELSE
      				
      				// Writes an event indicating that the service is started
      				ServiceWriteEventLog(“Beginning of service execution”, elInformation)
      			END
      		END
      END
     type : 458752
  procedure_templates : []
  property_templates : []
 code_parameters :
  internal_properties : CAAAAAgAAACVGQgYbalU7DKC3oH4ItDrjtcTHXXV/EuH8q0IbhyQlw==
  original_name : COL_Collection1
resources :
 string_res :
  identifier : 0x1006a1c334b90fa8
  internal_properties : CAAAAAgAAACm76HWfKGWp33VjXInA4cRlqArlgTTA862QGt72W2ld5Y=
custom_note :
 internal_properties : CAAAAAgAAABtB9HWVzrXO2+4NDRVK0vmzaNKrCKqH1DBX30lMmGZ
